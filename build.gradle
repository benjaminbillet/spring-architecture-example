plugins {
  id 'java'
  id 'org.springframework.boot' version '2.1.3.RELEASE' // the spring boot plugin
  id 'net.ltgt.apt' version '0.21' // an annotation processor used for mapstruct
  id 'org.sonarqube' version '2.7' // static code analyzer
}

// apply the tasks defined in the java plugin
apply plugin: 'java'

// apply the spring/spring boot plugins
// (the io.spring.dependency-management plugin is a dependency of the org.springframework.boot plugin)
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

// apply sub-file
apply from: 'sonar.gradle'

// use maven central for downloading dependencies
repositories {
  mavenCentral()
}

// a few informations about our application
group = 'my.app'
version = '0.0.1-SNAPSHOT'
description = ''

// Java version compatibility
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// the application entry point
springBoot {
  mainClassName = 'my.app.App'
}

bootRun {
  // if no SPRING_PROFILES_ACTIVE environment variable is set, the default will be local
  environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "local"
}

// variables
ext {
	mapstructVersion = '1.3.0.Final'
  problemSpringWebVersion = '0.24.0-RC.0'
  jwtVersion = '0.10.6'
}

dependencies {
  // we use the spring web starter: https://github.com/spring-projects/spring-boot/blob/2.1.x/spring-boot-project/spring-boot-starters/spring-boot-starter-web/pom.xml
  implementation 'org.springframework.boot:spring-boot-starter-web'

  // jpa starter
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

  // spring security
  implementation 'org.springframework.boot:spring-boot-starter-security'

  // spring actuator
  implementation 'org.springframework.boot:spring-boot-starter-actuator'

  // spring mail
  implementation 'org.springframework.boot:spring-boot-starter-mail'

  // spring templating
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

  // mariadb client
  implementation 'org.mariadb.jdbc:mariadb-java-client'

  // websocket support + web libraries
  implementation 'org.springframework.boot:spring-boot-starter-websocket'
  implementation 'org.springframework.security:spring-security-messaging'
  implementation 'org.webjars:webjars-locator-core'
  implementation 'org.webjars:sockjs-client:1.0.2'
  implementation 'org.webjars:stomp-websocket:2.3.3'
  implementation 'org.webjars:bootstrap:3.3.7'
  implementation 'org.webjars:jquery:3.1.0'
  implementation 'org.webjars.bower:axios:0.17.1'

  // caching
  implementation 'javax.cache:cache-api'
  implementation 'org.ehcache:ehcache'
  implementation "org.hibernate:hibernate-jcache"

  // jackson
  implementation "com.fasterxml.jackson.datatype:jackson-datatype-hppc" // high performance collections
  implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
  implementation "com.fasterxml.jackson.datatype:jackson-datatype-hibernate5"
  implementation "com.fasterxml.jackson.core:jackson-annotations"
  implementation "com.fasterxml.jackson.core:jackson-databind"
  implementation "com.fasterxml.jackson.module:jackson-module-afterburner"

  // mapstruct
  implementation "org.mapstruct:mapstruct-jdk8:${mapstructVersion}"
  annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

  // Hibernate JPA Model generator
  annotationProcessor 'javax.xml.bind:jaxb-api' // https://stackoverflow.com/a/52835834
  annotationProcessor 'org.hibernate:hibernate-jpamodelgen'

  // https://tools.ietf.org/html/rfc7807 implementation
  implementation "org.zalando:problem-spring-web:${problemSpringWebVersion}"

  // JSON Web Tokens
  implementation "io.jsonwebtoken:jjwt-api:${jwtVersion}"
  runtime "io.jsonwebtoken:jjwt-impl:${jwtVersion}"
  runtime "io.jsonwebtoken:jjwt-jackson:${jwtVersion}"

  // utils
  implementation "org.apache.commons:commons-lang3"
  implementation "org.apache.commons:commons-collections4:4.3"

  // monitoring
  implementation 'io.micrometer:micrometer-registry-prometheus'

  // auditing
  implementation "org.hibernate:hibernate-envers"

  // testing
  testCompile 'org.springframework.boot:spring-boot-starter-test'
  testCompile 'org.springframework.security:spring-security-test'
  testCompile 'org.springframework.boot:spring-boot-test'
  testCompile 'org.assertj:assertj-core'
  testCompile 'com.jayway.jsonpath:json-path'
  testCompile 'junit:junit'
  testCompile 'org.mockito:mockito-core'
  testCompile 'com.h2database:h2' // in-memory db for testing
}
